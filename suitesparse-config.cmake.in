if (CMAKE_VERSION VERSION_LESS 3.18)
  message (FATAL_ERROR "CMake >= 3.18 required")
endif (CMAKE_VERSION VERSION_LESS 3.18)

@PACKAGE_INIT@

list (APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

include (CMakeFindDependencyMacro)

find_dependency (BLAS)
find_dependency (LAPACK)
@CUDA_DEPENDENCY@
@METIS_DEPENDENCY@
@OpenMP_DEPENDENCY@
@TBB_DEPENDENCY@

# Add the targets after the dependecies were found since SuiteSparse targets
# depend on the external targets.
include ("${CMAKE_CURRENT_LIST_DIR}/suitesparse-targets.cmake")

set (_SuiteSparse_NAMESPACE "@SuiteSparse_NAMESPACE@")

foreach (_comp ${SuiteSparse_FIND_COMPONENTS})
  set (_TARGET ${_SuiteSparse_NAMESPACE}${_comp})

  if (TARGET ${_TARGET})
    set (SuiteSparse_${_comp}_FOUND TRUE)
  else (TARGET ${_TARGET})
    set (SuiteSparse_${_comp}_FOUND FALSE)
    set (SuiteSparse_NOT_FOUND_MESSAGE "SuiteSparse could not be found because the component ${_comp} could not be found.")
  endif (TARGET ${_TARGET})
endforeach (_comp)

check_required_components (SuiteSparse)
